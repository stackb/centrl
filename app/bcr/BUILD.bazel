load("@bazel_skylib//rules:common_settings.bzl", "string_flag")
load("@rules_tsickle//rules:defs.bzl", "closure_ts_compile")
load(
    "@stackb_rules_closure//closure:defs.bzl",
    "closure_css_binary",
    "closure_css_library",
    "closure_js_binary",
    "closure_js_library",
    "closure_js_template_library",
)
load("//rules:deploy.bzl", "cloudflare_deploy")
load("//rules:octicons_closure_template_compile.bzl", "octicons_closure_template_compile")
load("//rules:release.bzl", "release_archive")

string_flag(
    name = "release_type",
    build_setting_default = "debug",
    values = [
        "dummy",  # points to //data/dummy:modules - when submodule may not be initialized but still want to build //app/bcr:release
        "debug",
        "production",
    ],
    visibility = ["//visibility:public"],
)

string_flag(
    name = "docs_release",
    build_setting_default = "on",
    values = [
        "off",
        "on",
    ],
    visibility = ["//visibility:public"],
)

config_setting(
    name = "is_docs_release",
    flag_values = {
        ":docs_release": "on",
    },
)

config_setting(
    name = "is_debug_release",
    flag_values = {
        ":release_type": "debug",
    },
)

config_setting(
    name = "is_dummy_release",
    flag_values = {
        ":release_type": "dummy",
    },
)

# NOTE: closure-templates seems to have issues compiling generated .soy files
# (or the import path becomes confusing). This rule builds the .soy generated
# file, and is also a runnable rule that copies it back into the source tree.
# Regenerate as needed.
#
# ProTip: bazel run //app/bcr:octicons
octicons_closure_template_compile(
    name = "octicons",
)

closure_css_library(
    name = "style_closure_css",
    srcs = ["style.closure.css"],
)

closure_css_binary(
    name = "css",
    debug = select({
        ":is_debug_release": True,
        "//conditions:default": False,
    }),
    renaming = True,
    visibility = ["//visibility:public"],
    deps = [":style_closure_css"],
)

closure_js_template_library(
    name = "soy",
    srcs = [
        "app.soy",
        "maintainers.soy",
        "octicons.soy",
        "registry.soy",
        "settings.soy",
        "treeview.soy",
        "uri.soy",
    ],
    deps = [
        "//build/stack/bazel/bzlmod/v1:bzpb_js",
        "//build/stack/starlark/v1beta1:v1beta1_proto_js",
        "//stardoc_output:stardoc_output_js",
    ],
)

closure_ts_compile(
    name = "shiki",
    srcs = ["shiki.d.ts"],
)

closure_js_library(
    name = "common",
    srcs = [
        "common.js",
        "mvs.js",
    ],
    deps = [
        "//build/stack/bazel/bzlmod/v1:bzpb_js",
        "@stackb_rules_closure//closure/goog/asserts",
        "@stackb_rules_closure//closure/goog/dom",
        "@stackb_rules_closure//closure/goog/ui/ac:autocomplete",
        "@stackb_rules_closure//closure/goog/ui/ac:cachingmatcher",
        "@stackb_rules_closure//closure/goog/ui/ac:inputhandler",
        "@stackb_rules_closure//closure/goog/ui/ac:renderer",
        "@stackb_ui_js//js/ui:core",
        "@stackb_ui_js//js/ui:keyboard",
        "@stackb_ui_js//js/ui:select",
    ],
)

closure_js_library(
    name = "autocompletematcher",
    srcs = [
        "autocompletematcher.js",
        "heap.js",
    ],
    deps = [
        "@stackb_rules_closure//closure/goog/asserts",
        "@stackb_rules_closure//closure/goog/string",
    ],
)

closure_js_library(
    name = "app",
    srcs = [
        "app.js",
        "body.js",
        "clipboard.js",
        "contentcomponent.js",
        "contentselect.js",
        "documentation.js",
        "documentation_search.js",
        "format.js",
        "home.js",
        "language.js",
        "main.js",
        "maintainers.js",
        "module_search.js",
        "modules.js",
        "mvs_tree.js",
        "registry.js",
        "search.js",
        "selectnav.js",
        "settings.js",
        "starlark.js",
        "syntax.js",
        "treeview.js",
        ":shiki",
    ],
    deps = [
        ":autocompletematcher",
        ":common",
        ":soy",
        "//build/stack/bazel/bzlmod/v1:bzpb_js",
        "//build/stack/starlark/v1beta1:v1beta1_proto_js",
        "//stardoc_output:stardoc_output_js",
        "@stackb_rules_closure//closure/goog/array",
        "@stackb_rules_closure//closure/goog/asserts",
        "@stackb_rules_closure//closure/goog/crypt:base64",
        "@stackb_rules_closure//closure/goog/date",
        "@stackb_rules_closure//closure/goog/date:relative",
        "@stackb_rules_closure//closure/goog/dom",
        "@stackb_rules_closure//closure/goog/dom:classlist",
        "@stackb_rules_closure//closure/goog/dom:dataset",
        "@stackb_rules_closure//closure/goog/events",
        "@stackb_rules_closure//closure/goog/events:eventtype",
        "@stackb_rules_closure//closure/goog/events:listenable",
        "@stackb_rules_closure//closure/goog/soy",
        "@stackb_rules_closure//closure/goog/string",
        "@stackb_rules_closure//closure/goog/string:path",
        "@stackb_rules_closure//closure/goog/structs:treenode",
        "@stackb_rules_closure//closure/goog/structs:trie",
        "@stackb_rules_closure//closure/goog/style",
        "@stackb_rules_closure//closure/goog/ui:component",
        "@stackb_rules_closure//closure/goog/ui/ac:autocomplete",
        "@stackb_rules_closure//closure/goog/ui/ac:renderer",
        "@stackb_rules_closure//closure/goog/ui/tree:treecontrol",
        "@stackb_rules_closure//closure/goog/ui/tree:treenode",
        "@stackb_rules_closure//closure/protobuf:jspb",
        "@stackb_rules_closure//google3/third_party/javascript/safevalues",
        "@stackb_rules_closure//google3/third_party/javascript/safevalues/dom/elements",
        "@stackb_ui_js//js/ui:core",
        "@stackb_ui_js//js/ui:select",
    ],
)

closure_js_binary(
    name = "bcr",
    compilation_level = "ADVANCED",  # "SIMPLE",
    css = ":css",
    debug = select({
        ":is_debug_release": True,
        "//conditions:default": False,
    }),
    entry_points = ["goog:bcr.main"],
    language = "ECMASCRIPT_2021",
    output_wrapper = "(function(){%output%}).call(this);" + select({
        ":is_debug_release": "\n\n//# sourceMappingURL=bcr.js.map",
        "//conditions:default": "",
    }),
    deps = [":app"],
)

# capture specific outputs of module_registry
[
    filegroup(
        name = output_group,
        srcs = select({
            ":is_dummy_release": ["//data/dummy:modules"],
            "//conditions:default": ["//data/bazel-central-registry/modules"],
        }),
        output_group = output_group,
    )
    for output_group in [
        "colors_css",
        "sitemap_xml",
        "robots_txt",
        "registry_pb",
        "documentation_registry_pb",
    ]
]

# concatenates css into a single file
genrule(
    name = "bcr_css",
    srcs = [
        ":colors_css",  # generated language colors
        ":css",  # css renamed rules
        "style.css",  # unrenamed rules
    ],
    outs = ["bcr.css"],
    cmd = "cat $(SRCS) > $@",
)

# builds the release.tar.
#
# ProTip: bazel run //app/bcr:release for development server
release_archive(
    name = "release",
    srcs = [
        "favicon.png",
        "registry_pb",
        ":robots_txt",
        ":sitemap_xml",
    ] + select({
        ":is_debug_release": ["bcr.js.map"],
        "//conditions:default": [],
    }),
    hashed_srcs = [
        ":bcr.css",
        ":bcr.js",
    ],
    index_html = "index.html",
    registry_file = ":registry_pb",
)

cloudflare_deploy(
    name = "deploy",
    account_id = "df53dccd72b4a61d2f8b1323256bc71c",
    project = "centrl",
    tarball = ":release",
)
