"bazel modules file"

module(
    name = "stackb_centrl",
    version = "0.0.0",
    compatibility_level = 1,
)

# -------------------------------------------------------------------
# Dev  Dependencies
# -------------------------------------------------------------------

bazel_dep(name = "bazel_skylib", version = "1.8.2", dev_dependency = True)
bazel_dep(name = "build_stack_rules_proto", version = "4.1.1", dev_dependency = True)
bazel_dep(name = "buildifier_prebuilt", version = "8.2.1.1", dev_dependency = True)
bazel_dep(name = "gazelle", version = "0.47.0", dev_dependency = True, repo_name = "bazel_gazelle")
bazel_dep(name = "rules_go", version = "0.59.0", dev_dependency = True)
bazel_dep(name = "rules_java", version = "8.16.1", dev_dependency = True)
bazel_dep(name = "rules_proto", version = "7.1.0", dev_dependency = True)
bazel_dep(name = "rules_tsickle", version = "1.1.0", dev_dependency = True)
bazel_dep(name = "stackb_rules_closure", version = "1.0.0", dev_dependency = True)
bazel_dep(name = "stackb_ui_js", version = "1.0.0", dev_dependency = True)

# -------------------------------------------------------------------
# Overrides
# -------------------------------------------------------------------

# Commit: 3bcf945833dc086651abb8ebfb83dc8bf9da90ce
# Date: 2025-12-13 15:49:51 +0000 UTC
# URL: https://github.com/stackb/rules_proto/commit/3bcf945833dc086651abb8ebfb83dc8bf9da90ce
#
# checkpoint functioning version
# Size: 4101266 (4.1 MB)
archive_override(
    module_name = "build_stack_rules_proto",
    sha256 = "ca886c5aed0a93ea2c10397c50f6d2743109347e52832096ebb2e1846ffa3b14",
    strip_prefix = "rules_proto-3bcf945833dc086651abb8ebfb83dc8bf9da90ce",
    urls = ["https://github.com/stackb/rules_proto/archive/3bcf945833dc086651abb8ebfb83dc8bf9da90ce.tar.gz"],
)

# Commit: 8c7ea42b02671b60182ce298a1f386bee9960136
# Date: 2025-12-10 18:31:16 +0000 UTC
# URL: https://github.com/stackb/rules_closure/commit/8c7ea42b02671b60182ce298a1f386bee9960136
#
# safevalues: suppress checktypes on elements
# Size: 5017676 (5.0 MB)
archive_override(
    module_name = "stackb_rules_closure",
    sha256 = "72db2d8f1fba23d87c41722b5ba87d08736e8a94e7a58bda8a6fe0076956dc65",
    strip_prefix = "rules_closure-8c7ea42b02671b60182ce298a1f386bee9960136",
    urls = ["https://github.com/stackb/rules_closure/archive/8c7ea42b02671b60182ce298a1f386bee9960136.tar.gz"],
)

# Commit: a306339df9da4f83594970b0406751798d9818c9
# Date: 2025-11-20 01:32:03 +0000 UTC
# URL: https://github.com/stackb/ui.js/commit/a306339df9da4f83594970b0406751798d9818c9
#
# Refactor tests location (#11)
# Size: 68907 (69 kB)
archive_override(
    module_name = "stackb_ui_js",
    sha256 = "82b2f5e96acb760291df86bfa991c863cb2f54ce1380f7bc6e5c901458517193",
    strip_prefix = "ui.js-a306339df9da4f83594970b0406751798d9818c9",
    urls = ["https://github.com/stackb/ui.js/archive/a306339df9da4f83594970b0406751798d9818c9.tar.gz"],
)

# NOTE: cannot upgrade past protobuf editions...
#
# Commit: 263ee701cba6b75e1f8eddad5adcdf74718318b1
# Date: 2025-10-08 21:32:22 +0000 UTC
# URL: https://github.com/protocolbuffers/protobuf-javascript/commit/263ee701cba6b75e1f8eddad5adcdf74718318b1
#
# remove writeZigzagVarint64BigInt
# Size: 401599 (402 kB)
archive_override(
    module_name = "protobuf_javascript",
    patch_strip = 1,
    patches = ["protobuf_javascript.patch"],
    sha256 = "8a50071fbca5e4a26361e6c9d81dd842207f0005f7cc1720226f20c25a231805",
    strip_prefix = "protobuf-javascript-263ee701cba6b75e1f8eddad5adcdf74718318b1",
    urls = ["https://github.com/protocolbuffers/protobuf-javascript/archive/263ee701cba6b75e1f8eddad5adcdf74718318b1.tar.gz"],
)

# -------------------------------------------------------------------
# Configuration: Go
# -------------------------------------------------------------------

go_sdk = use_extension("@rules_go//go:extensions.bzl", "go_sdk")
go_sdk.from_file(go_mod = "//:go.mod")
use_repo(
    go_sdk,
    go_sdk = "go_default_sdk",
)

go_deps = use_extension("@bazel_gazelle//:extensions.bzl", "go_deps")
go_deps.from_file(go_mod = "//:go.mod")
use_repo(
    go_deps,
    "com_github_amenzhinsky_go_memexec",
    "com_github_bazelbuild_buildtools",
    "com_github_chromedp_chromedp",
    "com_github_dominikbraun_graph",
    "com_github_google_go_github_v66",
    "com_github_junkblocker_codesearch",
    "com_github_rs_zerolog",
    "com_github_samthor_sre2",
    "com_github_schollz_progressbar_v3",
    "com_github_shurcool_githubv4",
    "com_github_teacat_noire",
    "com_github_zeebo_blake3",
    "in_gopkg_yaml_v3",
    "net_starlark_go",
    "org_golang_google_grpc",
    "org_golang_google_protobuf",
    "org_golang_x_oauth2",
    "org_golang_x_sync",
    "org_golang_x_time",
)

# -------------------------------------------------------------------
# Configuration: Registry Base File
# -------------------------------------------------------------------

http_file = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_file")

# Vendor in the last known state of the registry, as a backup source of metadata
# in case we hit rate limits while fetching github repository metadata.
#
# $ bazel vendor --vendor_dir=thirdparty --repo=@registry_pb
http_file(
    name = "registry_pb",
    downloaded_file_path = "registry.pb",
    urls = [
        "https://621c132f.centrl.pages.dev/registry.pb",
    ],
)

# -------------------------------------------------------------------
# Configuration: colors.json
# -------------------------------------------------------------------

http_archive = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

# Branch: master
# Commit: 427cd04c0c95613318b7eadf5da46e056b062d79
# Date: 2025-11-10 19:24:07 +0000 UTC
# URL: https://github.com/ozh/github-colors/commit/427cd04c0c95613318b7eadf5da46e056b062d79
#
# update colours
# Size: 25068 (25 kB)
http_archive(
    name = "com_github_ozh_github_colors",
    build_file_content = """
filegroup(
    name = "colors_json",
    srcs = ["colors.json"],
    visibility = ["//visibility:public"],    
)
""",
    sha256 = "719fb2723dac3ed23ccd2f4e97c29dc1878778f727bc33973a41908d82bbccbb",
    strip_prefix = "github-colors-427cd04c0c95613318b7eadf5da46e056b062d79",
    urls = ["https://github.com/ozh/github-colors/archive/427cd04c0c95613318b7eadf5da46e056b062d79.tar.gz"],
)

# -------------------------------------------------------------------
# Configuration: Additional BCR modules
# -------------------------------------------------------------------

# the gazelle extension modifies the use_repo(starlark_repository) call, so use
# a different extension target name for ones the gazelle extension should not
# touch.
starlark_repository_internal = use_extension("@build_stack_rules_proto//extensions:starlark_repository.bzl", "starlark_repository", dev_dependency = True)
starlark_repository_internal.archive(
    name = "bzl.bazel_tools",
    build_directives = [
        "gazelle:starlarkrepository_root tools",
        "gazelle:starlarkrepository_root src/main/starlark/builtins_bzl",
    ],
    build_file_generation = "clean",
    languages = ["starlarkrepository"],
    strip_prefix = "bazel-8.4.2",
    urls = ["https://github.com/bazelbuild/bazel/archive/refs/tags/8.4.2.zip"],
)
use_repo(
    starlark_repository_internal,
    "bzl.bazel_tools",
)

starlark_repository = use_extension("@build_stack_rules_proto//extensions:starlark_repository.bzl", "starlark_repository", dev_dependency = True)
use_repo(starlark_repository)
