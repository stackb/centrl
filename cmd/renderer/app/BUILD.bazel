load(
    "@io_bazel_rules_closure//closure:defs.bzl",
    "closure_css_binary",
    "closure_css_library",
    "closure_js_binary",
    "closure_js_library",
    "closure_js_template_library",
)
load("@rules_tsickle//rules:defs.bzl", "closure_ts_compile")
load("//rules:cloudflare.bzl", "cloudflare_deploy")

closure_css_library(
    name = "css",
    srcs = [
        "index.css",
    ],
)

closure_css_binary(
    name = "style",
    debug = False,
    renaming = True,
    visibility = ["//visibility:public"],
    deps = [":css"],
)

closure_js_template_library(
    name = "soy",
    srcs = [
        "app.soy",
        "registry.soy",
    ],
    deps = [
        "//build/stack/bazel/bzlmod/v1:bzpb_js",
    ],
)

closure_ts_compile(
    name = "shiki",
    srcs = ["shiki.d.ts"],
)

closure_js_library(
    name = "common",
    srcs = ["common.js"],
    deps = [
        "@build_stack_ui_js//js/ui:core",
        "@build_stack_ui_js//js/ui:keyboard",
        "@build_stack_ui_js//js/ui:select",
        "@io_bazel_rules_closure//closure/goog/dom",
        "@io_bazel_rules_closure//closure/goog/ui/ac:autocomplete",
        "@io_bazel_rules_closure//closure/goog/ui/ac:cachingmatcher",
        "@io_bazel_rules_closure//closure/goog/ui/ac:inputhandler",
        "@io_bazel_rules_closure//closure/goog/ui/ac:renderer",
    ],
)

closure_js_library(
    name = "search",
    srcs = [
        "autocompletematcher.js",
        "heap.js",
    ],
    deps = [
        "@io_bazel_rules_closure//closure/goog/asserts",
        "@io_bazel_rules_closure//closure/goog/string",
    ],
)

closure_js_library(
    name = "app",
    srcs = [
        "app.js",
        "search.js",
        "shiki.externs.js",
    ],
    deps = [
        ":common",
        ":search",
        ":soy",
        "//build/stack/bazel/bzlmod/v1:bzpb_js",
        "@build_stack_ui_js//js/ui:core",
        "@build_stack_ui_js//js/ui:select",
        "@io_bazel_rules_closure//closure/goog/array",
        "@io_bazel_rules_closure//closure/goog/asserts",
        "@io_bazel_rules_closure//closure/goog/dom",
        "@io_bazel_rules_closure//closure/goog/dom:dataset",
        "@io_bazel_rules_closure//closure/goog/events",
        "@io_bazel_rules_closure//closure/goog/events:listenable",
        "@io_bazel_rules_closure//closure/goog/soy",
        "@io_bazel_rules_closure//closure/goog/string",
        "@io_bazel_rules_closure//closure/goog/ui:component",
        "@io_bazel_rules_closure//closure/goog/ui/ac:autocomplete",
        "@io_bazel_rules_closure//closure/goog/ui/ac:cachingmatcher",
        "@io_bazel_rules_closure//closure/goog/ui/ac:inputhandler",
        "@io_bazel_rules_closure//closure/goog/ui/ac:renderer",
        "@io_bazel_rules_closure//closure/protobuf:jspb",
    ],
)

closure_js_library(
    name = "main",
    srcs = ["main.js"],
    deps = [
        ":app",
        "//build/stack/bazel/bzlmod/v1:bzpb_js",
        "@io_bazel_rules_closure//closure/goog/crypt:base64",
        "@io_bazel_rules_closure//closure/protobuf:jspb",
    ],
)

closure_js_binary(
    name = "bundle",
    compilation_level = "ADVANCED",
    # compilation_level = "SIMPLE",
    css = ":style",
    debug = True,
    defs = [
        "--define=GREETING=world",
    ],
    entry_points = ["goog:centrl.main"],
    language = "ECMASCRIPT_2021",
    output_wrapper = """
(function(){%output%}).call(this);
//# sourceMappingURL=bundle.js.map
""",
    deps = [":main"],
)

genrule(
    name = "registry",
    outs = ["registry.pb"],
    cmd = "cp $(location //modules) $@",
    tools = ["//modules"],
)

genrule(
    name = "registry_b64",
    srcs = ["registry.pb"],
    outs = ["registry.pb.gz.b64.js"],
    cmd = "echo 'const REGISTRY_DATA = \"'$$(gzip -c $< | base64)'\";' > $@",
)

genrule(
    name = "index_html",
    outs = ["index.html"],
    cmd = "cp $(location _index.html) $@",
    tools = ["_index.html"],
)

# ProTip: serve -p 8080 -S -s bazel-bin/cmd/renderer/app/web.runfiles/_main/cmd/renderer/app/
filegroup(
    name = "files",
    srcs = [
        "bundle.js",
        "bundle.js.map",
        "index_html",
        "primer.css",
        "registry.pb",
        "style.css",
        ":registry_b64",
    ],
)

cloudflare_deploy(
    name = "deploy",
    srcs = [":files"],
    project = "centrl",
)
