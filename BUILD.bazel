load("@bazel_gazelle//:def.bzl", "gazelle", "gazelle_binary")
load("@build_stack_rules_proto//rules:proto_gazelle.bzl", "proto_gazelle")

exports_files([
    "rules_proto_config.yaml",
    "protobuf_javascript.patch",
])

# ----------------------------------------------------
# Gazelle
# ----------------------------------------------------
#
# -- Gazelle language "walk" ---
# gazelle:exclude thirdparty
#
# -- Gazelle language "go" ---
# gazelle:prefix github.com/stackb/centrl
# gazelle:go_generate_proto false
#
# -- Gazelle language "protobuf" ---
# gazelle:proto_language go enable true

gazelle_binary(
    name = "gazelle-custom",
    languages = [
        "@bazel_gazelle//language/go",
        "@bazel_gazelle//language/proto",
        "@build_stack_rules_proto//language/protobuf",
    ],
    visibility = ["//visibility:public"],
)

proto_gazelle(
    name = "gazelle",
    cfgs = ["//:rules_proto_config.yaml"],
    data = ["@registry_pb//file"],
    external = "static",
    gazelle = "//:gazelle-custom",
)

gazelle_binary(
    name = "gazelle-bcr",
    languages = ["//language/bcr"],
    visibility = ["//visibility:public"],
)

bcr_exclude_dirs = [
    ".*",
    "app",
    "blaze",
    "build",
    "cmd",
    "language",
    "pkg",
    "rules",
    "thirdparty",
    "tools",
]

gazelle(
    name = "central_registry",
    args = [
        "--registry-file=$(rootpath @registry_pb//file)",
        "--registry-root=data/bazel-central-registry",
        "--registry-url=https://registry.bazel.build",
        "--exclude=.cache",  # exists in CI
        "--exclude=modules",
    ] + ["--exclude=" + dir for dir in bcr_exclude_dirs],
    data = ["@registry_pb//file"],
    gazelle = "//:gazelle-bcr",
)

gazelle(
    name = "local_registry",
    args = [
        "--registry-root=modules",
        "--registry-url=http://localhost:8080",
        "--exclude=data",
    ] + ["--exclude=" + dir for dir in bcr_exclude_dirs],
    gazelle = "//:gazelle-bcr",
)
